&---------------------------------------------------------------------*
*&      Form  GET_BASE_DATA_PARALL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_BASE_DATA_PARALL .
  DATA: LV_TASKNAME(10) TYPE C.
  DATA: LT_BASE_DATA TYPE TABLE OF TY_S_DISPLAY.
  DATA: LV_FPERIOD TYPE SPMON,
        LV_TPERIOD TYPE SPMON.
  DATA: LV_LINES TYPE I.

  LV_FPERIOD = P_GJAHR && '01'.
  LV_TPERIOD = P_YYYYMM.

  LOOP AT GT_ZFIT0241 INTO DATA(LS_ZFIT0241).
    CLEAR: LV_TASKNAME.
    LV_TASKNAME = 'ZFITP_' && LS_ZFIT0241-RCOMP.
    DO.
      DO.
        " 현재 작업중인 Process < 30 일때, 수행
        IF ( GV_SJOBS - GV_RJOBS ) < 30 .
          EXIT.
        ENDIF.
        WAIT UP TO '0.1' SECONDS.
      ENDDO.

      CALL FUNCTION 'ZFI_TP_COLLECT_PARAL_PROCESS'
        STARTING NEW TASK LV_TASKNAME
        DESTINATION IN GROUP GC_PARAL_GROUP
        PERFORMING RETURN_BASE_DATA ON END OF TASK
        EXPORTING
          IV_RCOMP              = LS_ZFIT0241-RCOMP
          IV_FPERIOD            = LV_FPERIOD
          IV_TPERIOD            = LV_TPERIOD
        TABLES
          OT_DATA               = LT_BASE_DATA
        EXCEPTIONS
          COMMUNICATION_FAILURE = 1
          SYSTEM_FAILURE        = 2
          RESOURCE_FAILURE      = 3.

      CASE SY-SUBRC.
        WHEN 0.
          " Administration of asynchronous tasks
          GV_SJOBS = GV_SJOBS + 1.
          CLEAR GV_EXCP_FLAG.  "Reset flag
          EXIT.  "Job processing finished

*        WHEN 1 OR 2.
          " Handling of communication and system failure

        WHEN 1 OR 2 OR 3.  "No resources available at present
          " Receive reply to asynchronous RFC calls
          IF GV_EXCP_FLAG = SPACE.
            GV_EXCP_FLAG = 'X'.
            " First attempt for RESOURCE_FAILURE handling
            WAIT UNTIL GV_RJOBS >= GV_SJOBS UP TO '0.01' SECONDS.
          ELSE.
            " Second attempt for RESOURCE_FAILURE handling
            WAIT UNTIL GV_RJOBS >= GV_SJOBS UP TO '0.1' SECONDS.
          ENDIF.

          IF SY-SUBRC = 0.
            CLEAR GV_EXCP_FLAG.  "Reset flag
          ELSE.  "No replies
          ENDIF.
      ENDCASE.
    ENDDO.
  ENDLOOP.

  WAIT UNTIL GV_RJOBS >= GV_SJOBS.
ENDFORM.
*&---------------------------------------------------------------------*
*&      Form  RETURN_BASE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
FORM RETURN_BASE_DATA USING PV_TASKNAME.
  DATA: LT_BASE_DATA TYPE TABLE OF TY_S_DISPLAY.

  CLEAR: LT_BASE_DATA.
  RECEIVE RESULTS FROM FUNCTION 'ZFI_TP_COLLECT_PARAL_PROCESS'
     TABLES
       OT_DATA = LT_BASE_DATA
     EXCEPTIONS
       COMMUNICATION_FAILURE = 1
       SYSTEM_FAILURE        = 2
       RESOURCE_FAILURE      = 3.

  GV_RJOBS = GV_RJOBS + 1.  "Receiving data
  GT_BASE_DATA = CORRESPONDING #( BASE ( GT_BASE_DATA )
                                         LT_BASE_DATA  ).
ENDFORM.
